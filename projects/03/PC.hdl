// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    // Choose increment in if `inc`, else use loop value
    Inc16 (in=loop, out=incremented);
    Mux16 (a=loop, b=incremented, sel=inc, out=choice1);

    // Choose load if `load`, else use previous choice
    Mux16 (a=choice1, b=in, sel=load, out=choice2);

    // Choose false in if `reset` else use previous choice
    Mux16 (a=choice2, b=false, sel=reset, out=choice3);

    Register (in=choice3, load=true, out=loop, out=out);
}
